// shadertype=hlsl
#include "common/common.hlsl"

struct ComputeInputType
{
	uint3 groupID : SV_GroupID;           // 3D index of the thread group in the dispatch.
	uint3 groupThreadID : SV_GroupThreadID;     // 3D index of local thread ID in a thread group.
	uint3 dispatchThreadID : SV_DispatchThreadID;  // 3D index of global thread ID in the dispatch.
	uint  groupIndex : SV_GroupIndex;        // Flattened local index of the thread within a thread group.
};

RWStructuredBuffer<Frustum> in_Frustums : register(u0);
RWStructuredBuffer<uint> g_LightIndexCounter : register(u1);
RWStructuredBuffer<uint> g_LightIndexList : register(u2);
RWTexture2D<uint2> g_LightGrid : register(u3);
RWTexture2D<float4> g_DebugTexture : register(u4);

Texture2D DepthTextureVS : register(t0);

groupshared uint l_uMinDepth;
groupshared uint l_uMaxDepth;
groupshared Frustum l_TileFrustum;
groupshared uint l_LightCount;
groupshared uint l_LightIndexStartOffset;
groupshared uint l_LightList[1024];

static float4 heatArray[8] = {
	float4(0.0f, 0.0f, 0.5f, 1.0f),
	float4(0.0f, 0.0f, 1.0f, 1.0f),
	float4(0.0f, 1.0f, 0.5f, 1.0f),
	float4(0.0f, 1.0f, 1.0f, 1.0f),
	float4(0.0f, 0.5f, 0.0f, 1.0f),
	float4(0.0f, 1.0f, 0.0f, 1.0f),
	float4(1.0f, 1.0f, 0.0f, 1.0f),
	float4(1.0f, 0.0f, 0.0f, 1.0f)
};

void AppendLight(uint lightIndex)
{
	uint index; // Index into the visible lights array.
	InterlockedAdd(l_LightCount, 1, index);
	if (index < 1024)
	{
		l_LightList[index] = lightIndex;
	}
}

[numthreads(BLOCK_SIZE, BLOCK_SIZE, 1)]
void main(ComputeInputType input)
{
	// Calculate min & max depth in threadgroup / tile.
	int2 texCoord = input.dispatchThreadID.xy;
	float fDepth = DepthTextureVS.Load(int3(texCoord, 0)).r;

	uint uDepth = asuint(fDepth);

	// Initialize group local variables
	// Avoid contention by other threads in the group.
	if (input.groupIndex == 0)
	{
		l_uMinDepth = 0xffffffff;
		l_uMaxDepth = 0;
		l_LightCount = 0;
		l_LightIndexStartOffset = 0;
		l_TileFrustum = in_Frustums[input.groupID.x + (input.groupID.y * dispatchParams[1].numThreadGroups.x)];
	}

	GroupMemoryBarrierWithGroupSync();

	// Get min/max depth
	InterlockedMin(l_uMinDepth, uDepth);
	InterlockedMax(l_uMaxDepth, uDepth);

	GroupMemoryBarrierWithGroupSync();

	float fMinDepth = asfloat(l_uMinDepth);
	float fMaxDepth = asfloat(l_uMaxDepth);

	// Convert depth values to view space.
	float minDepthVS = ClipToView(float4(0, 0, fMinDepth, 1), sky_p_inv).z;
	float maxDepthVS = ClipToView(float4(0, 0, fMaxDepth, 1), sky_p_inv).z;
	float nearClipVS = ClipToView(float4(0, 0, 0, 1), sky_p_inv).z;

	// Clipping plane for minimum depth value
	Plane minPlane = { float3(0, 0, -1), -minDepthVS };

	// Cull point light
	for (uint i = input.groupIndex; i < NR_POINT_LIGHTS; i += BLOCK_SIZE * BLOCK_SIZE)
	{
		pointLight light = pointLights[i];
		// w component of luminance is the attenuation radius
		if (light.luminance.w > 0.0f)
		{
			float4 lightPos_VS = mul(light.position, cameraCBuffer.t);
			lightPos_VS = mul(lightPos_VS, cameraCBuffer.r);
			Sphere sphere = { lightPos_VS.xyz, light.luminance.w };
			if (SphereInsideFrustum(sphere, l_TileFrustum, nearClipVS, maxDepthVS))
			{
				if (!SphereInsidePlane(sphere, minPlane))
				{
					AppendLight(i);
				}
			}
		}
	}

	GroupMemoryBarrierWithGroupSync();

	// Only local thread 0 could write to the global light index counter and light grid
	if (input.groupIndex == 0)
	{
		InterlockedAdd(g_LightIndexCounter[0], l_LightCount, l_LightIndexStartOffset);
		g_LightGrid[input.groupID.xy] = uint2(l_LightIndexStartOffset, l_LightCount);
	}

	GroupMemoryBarrierWithGroupSync();

	// Write to global light index list
	for (int i = input.groupIndex; i < l_LightCount; i += BLOCK_SIZE * BLOCK_SIZE)
	{
		g_LightIndexList[l_LightIndexStartOffset + i] = l_LightList[i];
	}

	// Write to debug heat map texture
	g_DebugTexture[texCoord] = float4(0, 0, 0, 0);

	if (input.groupThreadID.x == 0 || input.groupThreadID.y == 0)
	{
		g_DebugTexture[texCoord] = float4(0, 0, 0, 0.9f);
	}
	else if (input.groupThreadID.x == 1 || input.groupThreadID.y == 1)
	{
		g_DebugTexture[texCoord] = float4(1, 1, 1, 0.5f);
	}
	else if (l_LightCount > 0)
	{
		float4 heat;

		if (l_LightCount >= 8)
		{
			heat = heatArray[7];
		}
		else
		{
			heat = heatArray[l_LightCount - 1];
		}
		g_DebugTexture[texCoord] = heat;
	}
	else
	{
		g_DebugTexture[texCoord] = float4(0, 0, 0, 1);
	}
}