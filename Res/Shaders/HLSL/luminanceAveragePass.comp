// shadertype=hlsl
#include "common/common.hlsl"

struct ComputeInputType
{
	uint3 groupID : SV_GroupID;
	uint3 groupThreadID : SV_GroupThreadID;
	uint3 dispatchThreadID : SV_DispatchThreadID;
	uint  groupIndex : SV_GroupIndex;
};

[[vk::binding(0, 0)]]
cbuffer PerFrameConstantBuffer : register(b0)
{
	PerFrame_CB g_Frame;
}

static const uint numHistoryFrames = 8;

[[vk::binding(0, 1)]]
RWStructuredBuffer<uint> in_histogram : register(u0);
[[vk::binding(1, 1)]]
RWStructuredBuffer<float> out_average : register(u1);

groupshared uint histogramShared[256];

[numthreads(16, 16, 1)]
void main(ComputeInputType input)
{
	uint countForThisBin = in_histogram.Load(input.groupIndex);
	histogramShared[input.groupIndex] = countForThisBin * input.groupIndex;

	GroupMemoryBarrierWithGroupSync();

	// Clear histogram entry for reuse
	in_histogram[input.groupIndex] = 0;

	[unroll]
	for (uint histogramSampleIndex = (256 >> 1); histogramSampleIndex > 0; histogramSampleIndex >>= 1)
	{
		if (input.groupIndex < histogramSampleIndex)
		{
			InterlockedAdd(histogramShared[input.groupIndex], histogramShared[input.groupIndex + histogramSampleIndex]);
		}
		GroupMemoryBarrierWithGroupSync();
	}

	// Compute Luminance & Moving Average
	if (input.groupIndex == 0)
	{
		float weightedLogAverage = (histogramShared[0] / max(g_Frame.viewportSize.x * g_Frame.viewportSize.y - (float)countForThisBin, 1.0)) - 1.0;
		float range = g_Frame.maxLogLuminance - g_Frame.minLogLuminance;
		float weightedAverageLuminance = exp2(weightedLogAverage / 254.0 * range + g_Frame.minLogLuminance);

		// Store new luminance value in cyclic buffer
		uint bufferIndex = g_Frame.frameIndex % numHistoryFrames;
		out_average[bufferIndex] = weightedAverageLuminance;

		// Compute moving average over last `numHistoryFrames`
		float sumLuminance = 0.0;
		for (uint i = 0; i < numHistoryFrames; i++)
		{
			sumLuminance += out_average[i];
		}
		float adaptedLuminance = sumLuminance / numHistoryFrames;

		// Store result at index 0 for easy access
		out_average[0] = adaptedLuminance;
	}
}
