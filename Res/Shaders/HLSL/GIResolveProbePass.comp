// shadertype=hlsl
#include "common/common.hlsl"

struct ComputeInputType
{
	uint3 groupID : SV_GroupID;           // 3D index of the thread group in the dispatch.
	uint3 groupThreadID : SV_GroupThreadID;     // 3D index of local thread ID in a thread group.
	uint3 dispatchThreadID : SV_DispatchThreadID;  // 3D index of global thread ID in the dispatch.
	uint  groupIndex : SV_GroupIndex;        // Flattened local index of the thread within a thread group.
};

RWStructuredBuffer<Probe> in_probes : register(u0);
RWStructuredBuffer<BrickFactor> in_brickFactors : register(u1);
RWStructuredBuffer<float4> in_brickIrradiance : register(u2);
//RWStructuredBuffer<float4> in_skyRadiance : register(u3);
RWTexture3D<float4> out_irradianceVolume : register(u3);

[numthreads(8, 8, 8)]
void main(ComputeInputType input)
{
	uint l_probeIndex = input.dispatchThreadID.x + (input.dispatchThreadID.y * dispatchParams[4].numThreads.x) + (input.dispatchThreadID.z * dispatchParams[4].numThreads.x * dispatchParams[4].numThreads.y);

	if (l_probeIndex < GICBuffer.workload.z)
	{
		Probe l_probe = in_probes[l_probeIndex];
		float4 Lo[6];

		for (uint i = 0; i < 6; i++)
		{
			float3 l_writeCoord = (l_probe.pos.xyz);

			Lo[i] = float4(0.0, 0.0, 0.0, 0.0);

			uint brickFactorRangeBegin = l_probe.brickFactorRange[i * 2];
			uint brickFactorRangeEnd = l_probe.brickFactorRange[i * 2 + 1];

			if (brickFactorRangeBegin != -1)
			{
				if (brickFactorRangeBegin != brickFactorRangeEnd)
				{
					for (uint j = brickFactorRangeBegin; j <= brickFactorRangeEnd; j++)
					{
						BrickFactor l_brickFactor = in_brickFactors[j];
						Lo[i] += in_brickIrradiance[l_brickFactor.brickIndex] * l_brickFactor.basisWeight;
					}

					Lo[i] /= (brickFactorRangeEnd - brickFactorRangeBegin + 1);
				}
				else
				{
					Lo[i] = in_brickIrradiance[in_brickFactors[brickFactorRangeBegin].brickIndex];
				}
			}

			Lo[i].w = l_probe.skyVisibility[i];

			l_writeCoord = l_writeCoord + float3(0, 0, float(i) * GICBuffer.probeCount.z);
			out_irradianceVolume[l_writeCoord] = Lo[i];
		}
	}
}